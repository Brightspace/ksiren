def group = "com.brightspace.ksiren"
def artifactName = "ksiren-okhttp3-plugin"
def vcsUrlSetting = "https://github.com/Brightspace/ksiren/"
def description = 'Adapter to enable KSiren to create Okhttp3 Requests out of Siren actions and fetch entities using an Okhttp3 client.'
def versionValue = "1.1.0-beta1"

dependencies {
	compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.51'
	compile 'com.squareup.okhttp3:okhttp:3.8.1'

	compileOnly project(":ksiren")
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['MyPublication']
	pkg {
		repo = artifactName
		name = artifactName
		userOrg = 'brightspace'
		licenses = ['Apache-2.0']
		vcsUrl = vcsUrlSetting
		version {
			name = versionValue
			desc = description
			released  = new Date()
			vcsTag = versionValue
		}
	}
}

// Create the pom configuration:
def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id System.getenv('DEVELOPER_ID')
			name System.getenv('DEVELOPER_NAME')
			email System.getenv('DEVELOPER_EMAIL')
		}
	}

	scm {
		url vcsUrlSetting
	}
}

// Create the publication with the pom configuration:
publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId group
			artifactId artifactName
			version versionValue
			pom.withXml {
				def root = asNode()
				root.appendNode('description', description)
				root.appendNode('name', artifactName)
				root.appendNode('url', vcsUrlSetting)
				root.children().last() + pomConfig
			}
		}
	}
}
