apply plugin: 'jacoco'
apply plugin: "com.github.kt3k.coveralls"

def group = "com.brightspace.ksiren"
def artifactName = "ksiren"
def vcsUrlSetting = "https://github.com/Brightspace/ksiren/"
def description = 'Main package of KSiren: A Kotlin library for parsing responses from Siren API endpoints and building Siren Action requests.'
def versionValue = "3.0.0"

dependencies {
	compile 'org.jetbrains.kotlin:kotlin-stdlib:1.3.31'

	testCompile project(':ksiren-moshi-adapter')
	testCompile project(':ksiren-okhttp3-plugin')
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.2"
	testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.2"
	testImplementation 'junit:junit:4.12'
	testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.3.2"
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
	testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
	testCompile 'org.mockito:mockito-core:2.8.47'
}

jacocoTestReport {
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
}

coveralls {
	jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['MyPublication']
	pkg {
		repo = artifactName
		name = artifactName
		userOrg = 'brightspace'
		licenses = ['Apache-2.0']
		vcsUrl = vcsUrlSetting
		version {
			name = versionValue
			desc = description
			released  = new Date()
			vcsTag = versionValue
		}
	}
}

// Create the pom configuration:
def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id System.getenv('DEVELOPER_ID')
			name System.getenv('DEVELOPER_NAME')
			email System.getenv('DEVELOPER_EMAIL')
		}
	}

	scm {
		url vcsUrlSetting
	}
}

// Create the publication with the pom configuration:
publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId group
			artifactId artifactName
			version versionValue
			pom.withXml {
				def root = asNode()
				root.appendNode('description', description)
				root.appendNode('name', artifactName)
				root.appendNode('url', vcsUrlSetting)
				root.children().last() + pomConfig
			}
		}
	}
}
