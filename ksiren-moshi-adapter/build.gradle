def group = "com.brightspace.ksiren"
def artifactName = "ksiren-moshi-adapter"
def vcsUrlSetting = "https://github.com/Brightspace/ksiren/"
def description = 'Adapter to enable KSiren to parse Siren entities from JSON using Moshi.'
def versionValue = "1.1.0-beta1"

dependencies {
	compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.3-2'
	compile 'com.squareup.moshi:moshi:1.5.0'
	implementation 'javax.annotation:jsr250-api:1.0'

	compileOnly project(":ksiren")
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['MyPublication']
	pkg {
		repo = artifactName
		name = artifactName
		userOrg = 'brightspace'
		licenses = ['Apache-2.0']
		vcsUrl = vcsUrlSetting
		version {
			name = versionValue
			desc = description
			released  = new Date()
			vcsTag = versionValue
		}
	}
}

// Create the pom configuration:
def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id System.getenv('DEVELOPER_ID')
			name System.getenv('DEVELOPER_NAME')
			email System.getenv('DEVELOPER_EMAIL')
		}
	}

	scm {
		url vcsUrlSetting
	}
}

// Create the publication with the pom configuration:
publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId group
			artifactId artifactName
			version versionValue
			pom.withXml {
				def root = asNode()
				root.appendNode('description', description)
				root.appendNode('name', artifactName)
				root.appendNode('url', vcsUrlSetting)
				root.children().last() + pomConfig
			}
		}
	}
}
